#lang ivy1.7

include sort
include node
include network
include assertion

# Ivy checks that the following invariants always hold if we start with the initialization state
# (i.e., `after init`) and continuously apply the exported actions below.

# We need a bunch of auxiliary invariants for the prover to avoid spurious CTIs
invariant [heard_vote_implies_voted] assertion.heard_vote_implies_voted
invariant [heard_accept_implies_accepted] assertion.heard_accept_implies_accepted
invariant [accept_means_at_least_one_vote] assertion.accept_means_at_least_one_vote
invariant [if_node_is_ready_to_accept_it_must_accept] assertion.if_node_is_ready_to_accept_it_must_accept
invariant [if_node_is_ready_to_confirm_it_must_confirm] assertion.if_node_is_ready_to_confirm_it_must_confirm
invariant [accepted_implies_node_heard_itself_accept] assertion.accepted_implies_node_heard_itself_accept
invariant [voted_implies_node_heard_itself_vote] assertion.voted_implies_node_heard_itself_vote
invariant [confirmed_implies_it_heard_at_least_one_node_accept] assertion.confirmed_implies_it_heard_at_least_one_node_accept
invariant [accepted_implies_at_least_one_node_voted] assertion.accepted_implies_at_least_one_node_voted
invariant [confirmed_implies_accepted] assertion.confirmed_implies_accepted
invariant [confirmed_implies_there_exists_quorum_accepting_value] assertion.confirmed_implies_there_exists_quorum_accepting_value
invariant [heard_quorum_accept_implies_confirmed] assertion.heard_quorum_accept_implies_confirmed

# The theorem that we ultimately want to prove.
# I have no explanation, but the proof is significantly (~40%) faster if we spell out the condition
# for confirm_same_after_sufficient_messages than using
# assertion.confirm_same_after_sufficient_messages.
invariant [confirm_same_after_sufficient_messages]
        forall VAL. (exists NODE1, NODE2. node.confirmed(NODE1, VAL) & ~node.confirmed(NODE2, VAL))
            -> (exists NODE1, NODE2. node.accepted(NODE1, VAL) & ~node.heard_accept(NODE2, NODE1, VAL))

export network.deliver_vote
export network.deliver_accept
export node.vote
